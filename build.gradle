buildscript {
    ext.kotlin_version = '1.2.51'
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'

// Versions follow this format: MCVERSION-MAJORMOD.MAJORAPI.MINOR.PATCH.
version = "1.12.2-0.0.15.1"
group = "com.deflatedpickle.bugmagic"
archivesBaseName = "bugmagic"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2749"
    runDir = "run"

    mappings = "snapshot_20171002"
}

repositories {
    jcenter()
    maven { url = "http://maven.shadowfacts.net/" }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        url = 'https://repo.elytradev.com/'
    }
    maven {
        url "https://maven.mcmoddev.com/"
    }
    maven {
        url 'http://tehnut.info/maven/'
    }
    maven {
        url 'https://jitpack.io'
    }
    mavenCentral()
}

configurations {
    contained {
        transitive = false
    }
    compile.extendsFrom contained
}

dependencies {
    compile "net.shadowfacts:Forgelin:1.6.0"
    deobfCompile "autoreglib:AutoRegLib:1.3:18"
    deobfCompile "picklelib:picklelib:1.12.2:1.0.15.2"
    contained "com.github.Ladysnake:Ladylib:2.3.2"
    deobfCompile "info.amerifrance.guideapi:Guide-API:1.12-2.1.7-62"
    compileOnly 'com.elytradev:mirage:2.0.3-rc3-SNAPSHOT'
    // deobfCompile 'albedo:albedo:0.1.3'
    compile "net.ilexiconn:llibrary:1.7.9-1.12.2:dev"

    // Tabula
    deobfCompile "ichunutil:iChunUtil:1.12.2:7.1.4"
    deobfCompile "tabula-minecraft-modeler:Tabula:1.12.2:7.0.0"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from(configurations.contained.files) {
        include '*'
        into 'META-INF/libraries'
    }
    manifest {
        attributes([
                'ContainedDeps': configurations.contained.files.collect { it.name }.join(" "),
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}