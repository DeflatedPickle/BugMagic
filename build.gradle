buildscript {
    ext.kotlin_version = '1.3.50'
    ext.dokka_version = '0.10.1'

    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }

    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true) {
            exclude group: 'trove', module: 'trove'
        }
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:4.0.0"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "com.diffplug.gradle.spotless" // uwu mwy cwode wis wal cwean!!
apply plugin: 'org.jetbrains.dokka'

// Versions follow this format: MCVERSION-MAJORMOD.MAJORAPI.MINOR.PATCH.
version = "1.12.2-1.0.0.0"
group = "com.deflatedpickle.bugmagic"
archivesBaseName = "bugmagic"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    mappings channel: "stable", version: '39'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                heartdrops {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                heartdrops {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'heartdrops', '--all', '--output', file('src/generated/resources/')

            mods {
                heartdrops {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    jcenter()
    maven { url = "http://maven.shadowfacts.net/" }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven { url = 'https://repo.elytradev.com/' }
    maven { url = "https://maven.mcmoddev.com/" }
    mavenCentral()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2847'

    implementation "net.shadowfacts:Forgelin:1.8.4"
    implementation "picklelib:picklelib:1.12.2:1.0.15.2"
    implementation "modelloader:modelloader:1.1.7"
    // implementation 'com.elytradev:mirage:2.0.3-rc3-SNAPSHOT'
    // implementation "net.ilexiconn:llibrary:1.7.19-1.12.2:dev"
    // implementation "guide-api:Guide-API-1.12:2.1.6:61"
    implementation "glasspane:glasspane:1.12.2:1.0.1"
    // implementation "ctm:CTM:MC1.12.2:0.3.3.22"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

jar {
    manifest {
        attributes 'FMLAT': 'bugmagic_at.cfg'
    }
}

spotless {
    java {
        googleJavaFormat()

        licenseHeader '/* Copyright (c) $YEAR DeflatedPickle under the MIT license */\n\n'
    }

    kotlin {
        ktlint()

        licenseHeader '/* Copyright (c) $YEAR DeflatedPickle under the MIT license */\n\n'
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = 'docs'

    configuration {
        reportUndocumented = true

        targets = ["JVM"]
        platform = "JVM"
    }
}